// FOS Server
// Author: heX, Ra
// Универсальный скрипт поведения охраны

class CGuards
{
	int Dialog;						// Номер диалога откуда беруться сообщения
	int SaveZoneGameVar;			// Идентификатор переменной, обозначающая что игрок находится в охраняемой зоне
	
	// инициализация
	CGuards()
	{
		Dialog=0;
		SaveZoneGameVar=0;
	}

	// Сообщения получаемые охранником
	void EventOnMessageToGumanoidGuards(::Critter& cr, ::Critter& fromCr, int message, int value)
	{		
		// Убить меня
		if(message==MESSAGE_KILL_ME)
		{
			::NpcPlane@ plane=::CreatePlane();
			plane.Type=AI_PLANE_ATTACK;
			plane.Priority=AI_PLANE_ATTACK_PRIORITY;
			plane.Attack_TargId=fromCr.Id;
			plane.Attack_MinHp=::__DeadHitPoints;
			plane.Attack_IsGag=false;
			plane.Attack_GagHexX=0;
			plane.Attack_GagHexY=0;
			plane.Attack_LastHexY=fromCr.HexY;
			plane.Attack_LastHexX=fromCr.HexX;
			plane.Run=true;
			cr.AddPlane(plane);
			return;
		}
		
		// Я несу нелегальные предметы
		if(message==MSG_IM_HAVE_ILLEGAL_ITEMS)
		{
			NpcPlanes::AddAttackPlane(cr, 0, value, true);
			::Critter @ target=::GetCritter(value);
			if (!valid(target)) return;
			if(target.IsNpc()) return;
			if(::Random(0,4)==1) cr.SayMsg(SAY_SHOUT, TEXTMSG_TEXT, STR_NO_ILLEGAL_ITEMS, ::GetPlayerName(value));
			return;
		}

		if( message == MSG_CANCEL_PICKUP )
		{
			::array<::NpcPlane@> planes = {};
			uint count=cr.GetPlanes(planes);
			if(count==0) return ;
			uint erased=0;
			for(uint i=0; i<count; i++)
				if( valid(planes[i]) && planes[i].Identifier==PLANE_LOOT && planes[i].IdentifierExt==uint(value) && cr.ErasePlane(i-erased)) erased++;

			if(cr.GetPlanes(PLANE_LOOT, value, planes)==0) return;
			for(uint i=0, iMax=planes.length(); i<iMax; i++)
				if(valid(planes[i]))
					cr.ErasePlane(i);
			return;
		}
		
		// Я заблокировал двери
		if(message==MESSAGE_I_AM_BLOCK_DOOR)
		{
			if(::Random(0,4)==1) cr.SayMsg(SAY_SHOUT, TEXTMSG_DLG, DLGSTR(cr.Stat[ST_DIALOG_ID], 9));
			return;
		}
	}
}
