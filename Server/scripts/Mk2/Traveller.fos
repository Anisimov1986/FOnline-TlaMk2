// FOS Server
// Author: rifleman17
// Скрипт для реализации нпц-путешественников. (Beta)
// НПЦ перемещаются между различными городами по глобальной карте в случайном порядке.
// НПЦ либо честно идет по глобальной карте по прямой, и при заходе на энкаунтер разбирается с мобами, затем продолжает движение
// Либо просто телепортируется в следующую точку маршрута.
// НПЦ находится в каждом городе определенное время.
// Список точек маршрута и настройки нпц сохраняются в AnyData для каждого нпц.
// Следующая точка маршрута выбирается в момент достижения предыдущей.
// Существует проблема: путешественник может застрять в некоем месте, например, на береговой линии, если пойдет из СФ в ароййо
// Поэтому при отправлении засекаем время начала движения, и назначаем время телепорта путешественника в точку назначения через 1 игровой месяц
// Traveller
enum Define
{
    TypeTransit      = 0 ,
    TypeTravel       = 1 ,
    CteFoundLoc     = 11 ,
    CteNewTravel    = 12 ,
    CteCheck         = 17 ,                                      // Событие проверки, достиг ли НПЦ точки назначения
    CteShowLoc      = 18 ,                                      // Подсветка энкаунтеров Илая на глобальной карте
    TimeCheck        = 7 * 24 * 60 * 60 ,                        // Время проверки в секундах
    TimeShowLoc      = 60 ,                                      // отображение локации через игровую минуту
    DistMax          = 10                                      // Минимальная дистанция в гексах до криттера, находясь на которой игрок попадет ему в группу при начале путешествия
}

uint get_NextTry()
{
	return REAL_MINUTE( 10 );
}

::string get_AnyDataKey( uint crId )
{
	return "Traveller_" + crId;
}

uint get_LocName( uint locPid )
{
	return ( ( locPid + 100 ) * 1000 );
}

// Присваевает скрипт для НПЦ и запускает его выполнение
// crId - Id нпц
// isTransit - признак, нпц перемещается телепортом
// entireNum - номер гекса, на который будет перемещаться нпц при заходе в локацию
// timeWaitLoc - время ожидания на локации до следующего путешествия
// timeWaitEnc - время ожидания на энкаунтере после завершения боя до продолжения путешествия
// locPids - массив кодов протоипов локаций, на которые может зайти НПЦ
void SetTraveller( uint crId, bool isTransit, uint entireNum, uint timeWaitLoc, uint timeWaitEnc, uint16[] locPids, bool visibleEncounters ) // Export
{
    ::Critter@ cr = ::GetCritter( crId );
    if( !valid( cr ) )
        return;
    cr.ModeBase[ MODE_GECK ] = 1;
    // cr.ModeBase[MODE_NO_HOME]=1;
    Traveller::Traveller@ t = Traveller::Traveller( crId );
    t.TravelType = ( isTransit ? Traveller::Define::TypeTransit : Traveller::Define::TypeTravel );
    t.EntireTo = entireNum;
    t.TimeWaitLocation = timeWaitLoc;
    t.TimeWaitEncounter = timeWaitEnc;
    t.VisibleEncounters = visibleEncounters;
    for( uint i = 0, iMax = locPids.length(); i < iMax; i++ )
    {
        t.AddLocPid( locPids[ i ] );
    }
    t.Save();
    if( valid( cr.GetMap() ) )
    {
        t.SheduleChooseLocation();
        t.SheduleTravel( true );
    }
    if( t.TravelType == Traveller::Define::TypeTravel )
    {
        cr.SetEvent( CRITTER_EVENT_GLOBAL_INVITE, "Traveller::_GlobalInvite" );
        cr.SetEvent( CRITTER_EVENT_GLOBAL_PROCESS, "Traveller::_GlobalProcess" );
    }
}

void CancelTraveller( uint crId )
{
    Traveller::Traveller@ ti = Traveller::Traveller( crId );
    ti.Erase();
    ::Critter@   cr = ::GetCritter( crId );
    if( !valid( cr ) )
        return;
    if( cr.GetTimeEvents( Traveller::Define::CteFoundLoc, null, null, null ) != 0 )
        cr.EraseTimeEvents( Traveller::Define::CteFoundLoc );
    if( cr.GetTimeEvents( Traveller::Define::CteNewTravel, null, null, null ) != 0 )
        cr.EraseTimeEvents( Traveller::Define::CteNewTravel );
}

class Traveller
{
    Traveller( uint crId )
    {
        this.CritterId = crId;
    }

    uint  CritterId;                // Id нпц-путешественника
    uint8 TravelType;               // Тип перемещений.(Телепорт или путешествие через глобал)
    uint  MapIdTo;                  // Id карты на которую направляется НПЦ
    uint  EntireTo;                 // Номер гекса, на котором будет стоять НПЦ
    uint  TimeWaitLocation;         // Время ожидания в игровых секундах на локации перед следующим путешествием
    uint  TimeWaitEncounter;        // Время ожидания в игровых секундах на энкаунтере во время путешествия
    bool  VisibleEncounters;        // Признак - делать ли энкаунтер видимым, если на него зашел НПЦ
    uint  TimeLastStart;            // Время последнего отправления НПЦ из локации
    uint  TimeNextRoute;            // Время следующего отправления НПЦ из локации
    uint16[] LocationPids;          // Массив кодов прототипов локаций, на которые может отправиться НПЦ
    uint[]   Followers;             // Массив Id спутников путешественника, может быть как игроки, так и нпц

    void AddLocPid( uint16 locPid )
    {
        for( uint i = 0, iMax = this.LocationPids.length(); i < iMax; i++ )
        {
            if( locPid == this.LocationPids[ i ] )
                return;
        }
        this.LocationPids.insertLast( locPid );
    }

    void AddFollower( uint crId )
    {
        for( uint i = 0, iMax = this.Followers.length(); i < iMax; i++ )
        {
            if( crId == this.Followers[ i ] )
                return;
        }
        this.Followers.insertLast( crId );
        this.Save();
    }

    // Сериализация

    void Save()
    {
        Serializator::Serializator save;
        save.Set( this.CritterId );
        save.Set( this.TravelType );
        save.Set( this.MapIdTo );
        save.Set( this.EntireTo );
        save.Set( this.TimeWaitLocation );
        save.Set( this.TimeWaitEncounter );
        save.Set( this.VisibleEncounters );
        save.Set( this.TimeLastStart );
        save.Set( this.TimeNextRoute );
        save.Set( this.LocationPids );
        save.Set( this.Followers );
        save.Save( AnyDataKey[ this.CritterId ] );
    }

    void Load()
    {
        Serializator::Serializator load;
        if( !load.Load( AnyDataKey[ this.CritterId ] ) )
            return;
        load.Get( this.CritterId );
        load.Get( this.TravelType );
        load.Get( this.MapIdTo );
        load.Get( this.EntireTo );
        load.Get( this.TimeWaitLocation );
        load.Get( this.TimeWaitEncounter );
        load.Get( this.VisibleEncounters );
        load.Get( this.TimeLastStart );
        load.Get( this.TimeNextRoute );
        load.Get( this.LocationPids );
        load.Get( this.Followers );
    }

    void Erase()
    {
        ::string key = AnyDataKey[ this.CritterId ];
        if( ::IsAnyData( key ) )
            ::EraseAnyData( key );
    }

    ::Critter@ GetTraveller()
    {
        if( this.CritterId == 0 )
            return null;
        return ::GetCritter( this.CritterId );
    }

    // Попытка найти новую точку перемещения
    bool TryFoundNewLocation()
    {
        int  tries = 20;
        uint idx = 0;
        uint len = this.LocationPids.length();
        uint maps = 0;
        if( len == 0 )
            return false;
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) )
            return false;
        uint mapId = 0;
        ::Map@ mapCr = cr.GetMap();
        if( valid( mapCr ) )
            mapId = mapCr.Id;
        while( tries > 0 )
        {
            idx = ::Random( 0, len - 1 );
            ::Location@[] locations;
            ::GetAllLocations( this.LocationPids[ idx ], locations );
            if( locations.length() > 0 )
            {
                idx = ::Random( 0, locations.length() - 1 );
                ::Location@ loc = locations[ idx ];
                if( valid( loc ) )
                {
                    maps = loc.GetMapCount();
                    for( uint i = 0; i < maps; i++ )
                    {
                        ::Map@ map = loc.GetMapByIndex( i );
                        if( valid( map ) && ( map.CountEntire( this.EntireTo ) > 0 ) && map.Id != mapId )
                        {
                            DL( "Target point found. Id=" + map.Id + "; ProtoMap=" + map.GetProtoId() );
                            this.MapIdTo = map.Id;
                            this.Save();
                            return true;
                        }
                    }
                }
            }
            tries--;
        }
        DL( "Travel point not found" );
        return false;
    }

    bool TransitToLocation()
    {
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) || this.MapIdTo == 0 )
            return false;
        ::Map@ map = ::GetMap( this.MapIdTo );
        if( !valid( map ) || map.CountEntire( this.EntireTo ) == 0 )
            return false;
        if( cr.TransitToMap( this.MapIdTo, this.EntireTo ) )
        {
            cr.SetHomePos( cr.HexX, cr.HexY, cr.Dir );
            return true;
        }
        return false;
    }

    // Собирает группу желающих и выходит на глобал
    // Если человек, желающий сопровождать нпц находится дальше Traveller::Define::DistMax от нпц, в группу не попадает
    bool BeginTravelToLocation()
    {
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) )
            return false;
        ::Map@ map = cr.GetMap();
        if( !valid( map ) )
            return false;
        ::Critter@[] grp = { @cr };
        uint[] confirmedIds;
        uint ch = cr.Stat[ ST_CHARISMA ];
        for( uint i = 0, iMax = this.Followers.length(); i < iMax && confirmedIds.length() < ch; i++ )
        {
            ::Critter@ follower = ::GetCritter( this.Followers[ i ] );
            if( valid( follower ) && follower.IsLife() && valid( follower.GetMap() ) && ( follower.GetMap().Id == map.Id ) && ( ::GetCrittersDistantion( cr, follower ) <= uint( Traveller::Define::DistMax ) ) )
            {
                grp.insertLast( @follower );
                confirmedIds.insertLast( follower.Id );
            }
        }
        this.Followers.resize( 0 );
        for( uint i = 0, iMax = confirmedIds.length(); i < iMax; i++ )
        {
            this.Followers.insertLast( confirmedIds[ i ] );
        }
        cr.TransitToGlobal( grp );
        this.TimeLastStart = ::__FullSecond;
        this.Save();
        return true;
    }

    void SheduleChooseLocation()
    {
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) )
            return;
        if( cr.GetTimeEvents( Traveller::Define::CteFoundLoc, null, null, null ) == 0 )
            cr.AddTimeEvent( "Traveller::cte_ChooseTravelPoint", 0, Traveller::Define::CteFoundLoc );
    }

    void SheduleTravel( bool fromLoc )
    {
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) )
            return;
        if( fromLoc && cr.GetTimeEvents( Traveller::Define::CteCheck, null, null, null ) != 0 )
            cr.EraseTimeEvents( Traveller::Define::CteCheck );           // В точку назначения прибыли, удаляем события проверки застрял/не застрял
        if( cr.GetTimeEvents( Traveller::Define::CteNewTravel, null, null, null ) == 0 )
        {
            cr.AddTimeEvent( "Traveller::cte_Travel", ( fromLoc ? this.TimeWaitLocation : this.TimeWaitEncounter ), Traveller::Define::CteNewTravel );
            this.TimeNextRoute = ::__FullSecond + ( fromLoc ? this.TimeWaitLocation : this.TimeWaitEncounter );
            DL( "time: " + this.TimeNextRoute + " or: " + Time::DateTimeToString( this.TimeNextRoute ) );
        }
        this.Save();
    }

    // Проверка маршрута и уточнение координат конечной точки маршрута
    // Актуально только для Traveller::Define::TypeTravel
    bool ValidateRoute( float& worldX, float& worldY )
    {
        if( this.MapIdTo == 0 )
            return false;
        ::Map@ map = ::GetMap( this.MapIdTo );
        if( !valid( map ) )
            return false;
        ::Location@ loc = map.GetLocation();
        if( !valid( loc ) )
            return false;
        worldX = loc.WorldX;
        ;
        worldY = loc.WorldY;
        return true;
    }

    // Проверка, что нпц не застрял на глобале
    void SetCheck()
    {
        ::Critter@ cr = this.GetTraveller();
        if( !valid( cr ) )
            return;
        uint[] indexes;
        if( cr.GetTimeEvents( Traveller::Define::CteCheck, indexes, null, null ) == 0 )
        {
            cr.AddTimeEvent( "Traveller::cte_Check", Traveller::Define::TimeCheck, Traveller::Define::CteCheck );
        }
        else
        {
            // Если событие уже существует, не отменяем
            // cr.ChangeTimeEvent(indexes[0], Traveller::Define::TimeCheck, 0);
        }
    }
}

Traveller::Traveller@ TravellerFromCritter( uint crId )
{
    ::string key = AnyDataKey[ crId ];
    if( !::IsAnyData( key ) )
        return null;
    Traveller::Traveller@ t = Traveller::Traveller( crId );
    t.Load();
    return t;
}

// Начало путешествия. Либо телепорт, либо переход по глобалу
uint cte_Travel( ::Critter& cr, int identifier, int& rate, int& )
{
    if( !cr.IsNoPlanes() || !valid( cr.GetMap() ) )
        return NextTry;
    Traveller::Traveller@ t = TravellerFromCritter( cr.Id );
    DL( "travel started!" + "time next: " + t.TimeNextRoute + ":" + Time::DateTimeToString( ::__FullSecond ) );
    if( valid( t ) )
    {
        if( t.TravelType == Traveller::Define::TypeTransit )
        {
            if( !t.TransitToLocation() )
            {
                DL( "Error, can't transit to new location" );
                return NextTry;
            }
            else
            {
                t.SheduleChooseLocation();
                t.SheduleTravel( true );
            }
        }
        else
        {
            if( !t.BeginTravelToLocation() )
            {
                DL( "Error, can't begin travel to new location" );
                return NextTry;
            }
            else
            {
                t.SetCheck();
            }
        }
    }
    return 0;
}

// Проверка, достиг ли НПЦ последней точки назначения в отведенное время
// если нет, переносится c  помощью TransitToMap
uint cte_Check( ::Critter& cr, int identifier, int& rate, int& )
{
    ::Map@ map = cr.GetMap();
    if( valid( map ) && valid( map.GetLocation() ) && !map.GetLocation().AutoGarbage )
    {
        return 0;
    }
    Traveller::Traveller@ t = TravellerFromCritter( cr.Id );
    if( valid( t ) )
    {
        if( !t.TransitToLocation() )
        {
            DL( "Error, can't transit to new location" );
            return NextTry;
        }
        else
        {
            t.Followers.resize( 0 );
            t.Save();
            t.SheduleChooseLocation();
            t.SheduleTravel( true );
        }
    }
    return 0;
}

// Поиск новой точки маршрута нпц, производится при заходе в локацию
uint cte_ChooseTravelPoint( ::Critter& cr, int identifier, int& rate, int& )
{
    DL( "Sheduled" );
    Traveller::Traveller@ t = TravellerFromCritter( cr.Id );
    if( valid( t ) )
    {
        if( !t.TryFoundNewLocation() )
        {
            DL( "cannot foun new location" );
            return NextTry;
        }
    }
    return 0;
}

uint cte_ShowLoc( ::Critter& cr, int identifier, int& rate, int& )
{
    ::Map@ map = cr.GetMap();
    if( valid( map ) )
    {
        ::Location@ loc = map.GetLocation();
        if( valid( loc ) && !loc.Visible )
        {
            loc.Visible = true;
            return 0;
        }
    }
    return Traveller::Define::TimeShowLoc;
}

// Обработка событий
bool _GlobalProcess( ::Critter& cr, int type, ::Item@ car, float& x, float& y, float& toX, float& toY, float& speed, uint& encounterDescriptor, bool& waitForAnswer )
{
    // DL("type: " + type + "; coords " + x + ":" + y + "; pointTo " + toX + ":" + toY + "cr.Id" + cr.Id + "; speed" + speed);
    Traveller::Traveller@ t = TravellerFromCritter( cr.Id );
    if( encounterDescriptor != 0 )
    {
        return false;         // Энкаунтер
    }
    if( valid( t ) && type != GLOBAL_PROCESS_MOVE )
    {
        if( t.ValidateRoute( toX, toY ) )
        {
            if( toX == x && toY == y )       // Добрались до точки назначения
            {
                if( cr.TransitToMap( t.MapIdTo, t.EntireTo, true ) )
                {
                    cr.SetHomePos( cr.HexX, cr.HexY, cr.Dir );
                    t.Followers.resize( 0 );
                    t.Save();
                    t.SheduleChooseLocation();
                    t.SheduleTravel( true );
                    // Сброс проверок на застревание при достижении точки назначения
                    uint[] indexes;
                    if( cr.GetTimeEvents( Traveller::Define::CteCheck, indexes, null, null ) != 0 )
                        cr.EraseTimeEvents( Traveller::Define::CteCheck );
                    return true;
                }
            }
            else
            {
                speed = 2 * GLOBAL_MAP_BASE_SPEED;
                return true;
            }
        }
        else
        {
            t.SheduleChooseLocation();
        }
    }
    waitForAnswer = false;
    return false;
}

bool _GlobalInvite( ::Critter& cr, ::Item@ car, uint encounterDescriptor, int combatMode, uint& mapId, uint16& hexX, uint16& hexY, uint8& dir )
{
    DL( "global invite" );
    Traveller::Traveller@ t = TravellerFromCritter( cr.Id );
    if( valid( t ) )
    {
        t.SheduleTravel( false );
        if( cr.GetTimeEvents( Traveller::Define::CteShowLoc, null, null, null ) == 0 )
        {
            cr.AddTimeEvent( "Traveller::cte_ShowLoc", Traveller::Define::TimeShowLoc, Traveller::Define::CteShowLoc );
        }
    }
    return false;
}

// Диалоги
void r_AddFollower( ::Critter& player, ::Critter@ npc )
{
    if( !valid( npc ) )
        return;
    Traveller::Traveller@ t = TravellerFromCritter( npc.Id );
    if( !valid( t ) )
        return;
    if( valid( t ) )
    {
        t.AddFollower( player.Id );
    }
}

// Проверка, находится ли нпц на энкаунтере или в одной из локаций маршрута
bool d_OnEncounter( ::Critter& player, ::Critter@ npc )
{
    if( !valid( npc ) )
        return false;
    Traveller::Traveller@ t = TravellerFromCritter( npc.Id );
    if( valid( t ) )
    {
        ::Map@ map = npc.GetMap();
        if( !valid( map ) )
            return false;
        ::Location@ loc = map.GetLocation();
        if( !valid( loc ) )
            return false;
        return loc.AutoGarbage;
    }
    return false;
}

bool d_NotOnEncounter( ::Critter& player, ::Critter@ npc )
{
    if( !valid( npc ) )
        return false;
    Traveller::Traveller@ t = TravellerFromCritter( npc.Id );
    if( valid( t ) )
    {
        ::Map@ map = npc.GetMap();
        if( !valid( map ) )
            return false;
        ::Location@ loc = map.GetLocation();
        if( !valid( loc ) )
            return false;
        return !loc.AutoGarbage;
    }
    return false;
}

void r_BeginTravel( ::Critter& player, ::Critter@ npc )
{
    if( !valid( npc ) )
        return;
    Traveller::Traveller@ t = TravellerFromCritter( npc.Id );
    if( valid( t ) )
    {
        if( t.BeginTravelToLocation() )
            t.SetCheck();
    }
}

// Генерация лексем: куда и когда отправится путешественник
void dlg_WhereAndWhen( ::Critter& player, ::Critter@ npc, ::string@ lexems )
{
    if( !IS_DIALOG_GENERATED( lexems ) )
        return;
    if( !valid( npc ) )
        return;
    Traveller::Traveller@ t = TravellerFromCritter( npc.Id );
    if( !valid( t ) )
        return;
    lexems = "";
    if( t.MapIdTo != 0 )
    {
        ::Map@ map = ::GetMap( t.MapIdTo );
        if( valid( map ) )
        {
            ::Location@ loc = map.GetLocation();
            if( valid( loc ) )
                lexems = "$where@msg GM " + LocName[ loc.GetProtoId() ] + "@";
        }
    }
    if( t.TimeNextRoute != 0 )
    {
        lexems += "$when" + Time::DateTimeToString( t.TimeNextRoute );
    }
}
